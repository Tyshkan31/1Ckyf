@Локально
@Подписка(Событие{Пользователи.Объект.ПослеПодключения})
метод ИнициализацияАккаунта(Источник: Пользователи.Объект)
    если АккаунтПользователя(Источник.Ссылка) == Неопределено
        знч НовыйАккаунт = новый Аккаунты.Объект()
        НовыйАккаунт.Пользователь = Источник.Ссылка

        знч ТекПользовательСервиса = ПользователиСервиса.Найти(Источник.Ид)

        НовыйАккаунт.Наименование = ТекПользовательСервиса.Логин
        НовыйАккаунт.ЭлектроннаяПочта = ТекПользовательСервиса.ЭлектроннаяПочта
        НовыйАккаунт.Записать()
    ;
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод АккаунтПользователя(): Аккаунты.Ссылка?
    знч Пользователь = Пользователи.ТекущийПользователь
    возврат ПолучитьАккаунтПоПользователю(Пользователь)
;

@ВПроекте
@НаСервере @ДоступноСКлиента
метод АккаунтПользователя(Пользователь: Пользователи.Ссылка): Аккаунты.Ссылка?
    возврат ПолучитьАккаунтПоПользователю(Пользователь)
;

@Локально
метод ПолучитьАккаунтПоПользователю(Пользователь: Пользователи.Ссылка): Аккаунты.Ссылка?
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Аккаунты.Ссылка КАК Ссылка
        ИЗ
            Аккаунты КАК Аккаунты
        ГДЕ
            Аккаунты.Пользователь == %Пользователь
    }

    исп Результат = Запрос.Выполнить()

    знч Выборка = Результат.ПервыйИлиНеопределено()

    возврат Выборка?.Ссылка
;