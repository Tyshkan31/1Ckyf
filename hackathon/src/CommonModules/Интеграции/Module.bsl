//Прокси сервер засунул в пароль. Пример curl запроса
//curl "https://generativelanguage.googleapis.com/v1beta/models/gemini-2.5-flash:generateContent" \
//  -H "x-goog-api-key: $GEMINI_API_KEY" \
//  -H 'Content-Type: application/json' \
//  -X POST \
//  -d '{
//    "contents": [
//      {
//        "parts": [
//          {
//            "text": "Explain how AI works in a few words"
//          }
//        ]
//      }
//    ]
//  }'
Функция ПолучитьСообщениеGemini(ТекстСообщения) Экспорт
	НастройкиПодключения = Справочники.НастройкиПодключения.ПолучитьНастройкиПодключения(Справочники.НастройкиПодключения.ПодключениеGemini);
	НастройкиПодключения_Прокси = Справочники.НастройкиПодключения.ПолучитьНастройкиПодключения(Справочники.НастройкиПодключения.Прокси);
	Заголовки = НастройкиПодключения.Заголовки;
	Если ЗначениеЗаполнено(НастройкиПодключения.Пароль) Тогда
		ИнетрнетПрокси = новый ИнтернетПрокси;
		ИнетрнетПрокси.Установить("https" , НастройкиПодключения_Прокси.URL, НастройкиПодключения_Прокси.Порт, НастройкиПодключения_Прокси.Логин, НастройкиПодключения_Прокси.Пароль, Ложь);
	КонецЕсли;
	Соединение = Новый HTTPСоединение(НастройкиПодключения.URL, , ,,ИнетрнетПрокси,15,Новый ЗащищенноеСоединениеOpenSSL);
	ЗапросHTTP = новый HTTPЗапрос(НастройкиПодключения.ВызываемыеМетоды[0], Заголовки);
	
	ТекстЗапроса = новый Структура;
	ТекстЗапроса.Вставить("text" , ТекстСообщения);
	
	ЧастьЗапроса = новый Массив;
	ЧастьЗапроса.Добавить(ТекстЗапроса);
	
	ЧастиЗапроса = Новый Структура;
	ЧастиЗапроса.Вставить("parts", ЧастьЗапроса);
	
	КонтентЗапроса = новый Массив;
	КонтентЗапроса.Добавить(ЧастиЗапроса);
	
	КонтентыЗапроса = новый Структура;
	КонтентыЗапроса.Вставить("contents", КонтентЗапроса);
	
	ЗаписьJSON = новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
    ЗаписатьJSON(ЗаписьJSON, КонтентыЗапроса);
	
	СтрокаЗапроса = ЗаписьJSON.Закрыть();

	
	ЗапросHTTP.УстановитьТелоИзСтроки(СтрокаЗапроса);
	
	Ответ = Соединение.ОтправитьДляОбработки(ЗапросHTTP);
	
	Если НЕ Ответ.КодСостояния = 200 Тогда
		
		Возврат Ответ.ПолучитьТелоКакСтроку();
				
	КонецЕсли;  
	
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	
	ЧтениеJSON = новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
	РезультатЧтения = ПрочитатьJSON(ЧтениеJSON, Истина);
	ОтветМодели = РезультатЧтения.Получить("candidates")[0].Получить("content").Получить("parts")[0].Получить("text");
	Возврат ОтветМодели;
КонецФункции

Процедура ОбновитьСписокКлиентов() Экспорт
	CRMПодключение = Справочники.НастройкиПодключения.AmoCRM;
	НастройкиПодключения = ИнтеграцииПовтИсп.ПолучитьНастройкиПодключения(CRMПодключение);
	ТокенАвторизации = НастройкиПодключения.ТокенДоступа;
	Заголовки = новый Соответствие;
	Заголовки.Вставить("Authorization", ТокенАвторизации);
	ЗащищенноеСоединение = новый ЗащищенноеСоединениеOpenSSL;
	СоединениеHTTP = новый HTTPСоединение(НастройкиПодключения.URL,
										  ,
										  ,
										  ,
										  ,
										  30,
										  ЗащищенноеСоединение);
	HTTPЗапрос = новый HTTPЗапрос(НастройкиПодключения.ВызываемыеМетоды[0], Заголовки);
	Ответ = СоединениеHTTP.Получить(HTTPЗапрос);
	
	ЧтениеJSON = новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	РезультатЧтения = ПрочитатьJSON(ЧтениеJSON,Истина);
	ЧтениеJSON.Закрыть();
	
	СписокКонтактов = РезультатЧтения.Получить("_embedded").Получить("contacts");
	
	ТЗID = новый ТаблицаЗначений;
	ТЗID.Колонки.Добавить("IDКлиента", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(20)));
	СоответствиеПользователей = Новый Соответствие;
	Для каждого Контакт Из СписокКонтактов Цикл
		НСтр = ТЗID.Добавить();
		НСтр.IDКлиента = Контакт.Получить("id");		
		СоответствиеПользователей.Вставить(НСтр.IDКлиента, Контакт);
	КонецЦикла;
	
	ТЗ = ПолучитьАктуальныйСписокКонтактовCRM(ТЗID);
	//Заполним справочник.
	Для каждого Строка ИЗ ТЗ Цикл
		Если Строка.КлиентСоздан Тогда
			СправочникОбъект = Строка.Ссылка.ПолучитьОбъект();
		Иначе
			СправочникОбъект = Справочники.КлиентыCRM.СоздатьЭлемент();
		КонецЕсли;       
		
		Для каждого Элемент ИЗ СоответствиеПользователей.Получить(Строка.IDКлиента).Получить("custom_fields_values") Цикл
			Если Элемент.Получить("field_name") = "Должность" Тогда
				Продолжить;
			КонецЕсли;
			Нстр = СправочникОбъект.КонтактнаяИнформация.Добавить();
			
			Если Элемент.Получить("field_name") = "Телефон" Тогда
				
				Нстр.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонПользователя;
				Нстр.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
				Нстр.Представление = "Телефон";
				Нстр.НомерТелефона = Элемент.Получить("values")[0].Получить("value");				
			ИначеЕсли Элемент.Получить("field_name") = "Email" Тогда
				Нстр.Представление = "Электронная почта";
				Нстр.Вид = Справочники.ВидыКонтактнойИнформации.EmailПользователя;
				Нстр.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				НСтр.АдресЭП = Элемент.Получить("values")[0].Получить("value");
			КонецЕсли;
		КонецЦикла;
		СправочникОбъект.Код = Строка.IDКлиента;
		СправочникОбъект.Наименование = СоответствиеПользователей.Получить(Строка.IDКлиента).Получить("name");
		
		СправочникОбъект.Записать();
		
		
	КонецЦикла;
КонецПроцедуры                       

Функция ПолучитьАктуальныйСписокКонтактовCRM(ТЗID)
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЗID.IDКлиента КАК IDКлиента
	               |ПОМЕСТИТЬ ВТТЗ
	               |ИЗ
	               |	&ТЗID КАК ТЗID
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КлиентыCRM.Ссылка КАК Ссылка,
	               |	ИСТИНА КАК КлиентСоздан,
	               |	ВТТЗ.IDКлиента КАК IDКлиента
	               |ПОМЕСТИТЬ ВТПодитоги
	               |ИЗ
	               |	Справочник.КлиентыCRM КАК КлиентыCRM
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТЗ КАК ВТТЗ
	               |		ПО (ВТТЗ.IDКлиента = КлиентыCRM.Код)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	КлиентыCRM.Ссылка,
	               |	ЛОЖЬ,
	               |	ВТТЗ.IDКлиента
	               |ИЗ
	               |	Справочник.КлиентыCRM КАК КлиентыCRM
	               |		ПРАВОЕ СОЕДИНЕНИЕ ВТТЗ КАК ВТТЗ
	               |		ПО (ВТТЗ.IDКлиента = КлиентыCRM.Код)
	               |ГДЕ
	               |	КлиентыCRM.Ссылка ЕСТЬ NULL
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВТПодитоги.Ссылка КАК Ссылка,
	               |	ВТПодитоги.КлиентСоздан КАК КлиентСоздан,
				   |    ВТПодитоги.IDКлиента
	               |ИЗ
	               |	ВТПодитоги КАК ВТПодитоги";
	Запрос.УстановитьПараметр("ТЗID", ТЗID);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции
//ТекстВопроса - Строка - Вопрос пользователя.
Функция ПолучитьБыстрыйОтвет(ТекстВопроса) Экспорт   

	СоединениеHTTP = новый HTTPСоединение("localhost" , 8000);
	Заголовки = новый Соответствие;
	Заголовки.Вставить("Content-Type" , "application/json");
	
	ЗапросHTTP = новый HTTPЗапрос("/fast_answer" , Заголовки);
	
	СтруктураЗапроса = новый Структура;
	СтруктураЗапроса.Вставить("text", ТекстВопроса);
	
	ЗаписьJSON = новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();
    ЗаписатьJSON(ЗаписьJSON, СтруктураЗапроса);
	
	СтрокаЗапроса = ЗаписьJSON.Закрыть();
	ЗапросHTTP.УстановитьТелоИзСтроки(СтрокаЗапроса);
	Ответ = СоединениеHTTP.ОтправитьДляОбработки(ЗапросHTTP);
	
	Возврат Ответ.ПолучитьТелоКакСтроку();

КонецФункции

//Процедура получает сообщения из ТГбота
Процедура ПолучитьСообщенияТелеграм() Экспорт
	
	
	СостояниеНовоеОбращение  	   = Перечисления.СостоянияПользователей.СоздалНовоеОбращение;
	СостояниеТекущиеОбращение 	   = Перечисления.СостоянияПользователей.РаботаетСТекущимОбращением;
	СостояниеОжидаетНовоеОбращение = Перечисления.СостоянияПользователей.ОжидаетНовоеОбращение;
	СостояниеЗакрытаЗаявка		   = Перечисления.СостоянияПользователей.Закрыто;						
	
	ПараметрыПодключения = ИнтеграцииПовтИсп.ПолучитьНастройкиПодключения(Справочники.НастройкиПодключения.Telegram);
	ТокенБота = ПараметрыПодключения.ТокенДоступа;
	СоединениеHTTP = Новый HTTPСоединение(ПараметрыПодключения.URL, , , , , , новый ЗащищенноеСоединениеOpenSSL); 
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type" , "application/json");
	
	
	ЗапросHTTP = Новый HTTPЗапрос("bot" + ТокенБота + ПараметрыПодключения.ВызываемыеМетоды[0]+ "?offset=-1");
	
	Ответ = СоединениеHTTP.Получить(ЗапросHTTP);
	
	Если Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ПолученныеДанные = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
	Иначе 
		
		ДатаСеанса = ТекущаяДатаСеанса();
		СтруктураHTTPЗапроса = Новый Структура; 
		                                          
		СтруктураHTTPЗапроса.Вставить("ОбъектЗапроса", "Неопределено");	
		СтруктураHTTPЗапроса.Вставить("Действие", Перечисления.ДействияHTTPЗапросов.ПолучениеДанныхИзТГ);
		СтруктураHTTPЗапроса.Вставить("Код", Ответ.КодСостояния);
		СтруктураHTTPЗапроса.Вставить("Описание", Ответ.ПолучитьТелоКакСтроку());
		
		СРОО_ОбщегоНазначения.ЗаполнитьСтатусHTTPЗапроса(ДатаСеанса, СтруктураHTTPЗапроса.ОбъектЗапроса, СтруктураHTTPЗапроса);
		
		Возврат;
	КонецЕсли;
	
	ПолученныеСообщения = ПолученныеДанные.result;
	МассивНовыйЗаявок = новый Массив;
	
	Для каждого СообщениеОбъект ИЗ ПолученныеСообщения Цикл
		ТекстСообщения = СообщениеОбъект.message.text;
		idЮзера =СообщениеОбъект.message.chat.id;
		update_id = СообщениеОбъект.update_id;	
		НаборЗаписей = РегистрыСведений.СостояниеТГПользователя.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.idUser.Установить(idЮзера);
		НаборЗаписей.Прочитать();
		флВозвращеноВРаботу = Ложь;
		флЗакрыта = Ложь;
		Если ТекстСообщения = "/createnewtask" ИЛИ НаборЗаписей.Количество() = 0 Тогда
			СостояниеПользователя = СостояниеНовоеОбращение;       
		ИначеЕсли ТекстСообщения = "/approve" Тогда //Нужно только установить пользователю состояние "Закрыто"
			Если НЕ НаборЗаписей.Количество() = 0 И НаборЗаписей[0].ТекущееСостояние = СостояниеЗакрытаЗаявка Тогда
				Продолжить;
			ИначеЕсли НаборЗаписей.Количество() = 0 
				ИЛИ НЕ НаборЗаписей[0].ТекущееСостояние = Перечисления.СостоянияПользователей.ОцениваетВыполнение Тогда
					ТекстСообщения = "Невозможно выполнить действие.";
					ОтправитьСообщениеТелеграм(idЮзера, ТекстСообщения);					
					Продолжить;
			КонецЕсли;
			СостояниеПользователя = СостояниеЗакрытаЗаявка;	
			
			флЗакрыта = Истина;
		ИначеЕсли ТекстСообщения = "/cancel" Тогда //Нужно установить состояние "СостояниеТекущиеОбращение"
			Если НЕ НаборЗаписей.Количество() = 0 И НаборЗаписей[0].ТекущееСостояние = СостояниеЗакрытаЗаявка Тогда
				Продолжить;
			ИначеЕсли НаборЗаписей.Количество() = 0 
				ИЛИ НЕ НаборЗаписей[0].ТекущееСостояние = Перечисления.СостоянияПользователей.ОцениваетВыполнение Тогда
					ТекстСообщения = "Невозможно выполнить действие.";
					ОтправитьСообщениеТелеграм(idЮзера, ТекстСообщения);					
					Продолжить;
			КонецЕсли;
			СостояниеПользователя = СостояниеТекущиеОбращение;
			флВозвращеноВРаботу = Истина;	
		Иначе
			СостояниеПользователя = НаборЗаписей[0].ТекущееСостояние;
		КонецЕсли;
		
		Если СостояниеПользователя = СостояниеНовоеОбращение Тогда
			МассивНовыйЗаявок.Добавить(idЮзера);             
		КонецЕсли;	
		НаборЗаписей.Очистить();

		Запись = НаборЗаписей.Добавить();
		Запись.idUser = idЮзера;
		Запись.ТекущееСостояние = СостояниеПользователя;
		
		НаборЗаписей.Записать(Истина);
		
		Если Запись.ТекущееСостояние = СостояниеОжидаетНовоеОбращение Тогда //Нужно создать новую заявку
			СоздатьНовуюЗаявку(ТекстСообщения, idЮзера);		
		ИначеЕсли Запись.ТекущееСостояние = СостояниеТекущиеОбращение И НЕ флВозвращеноВРаботу И НЕ флЗакрыта Тогда
			ДобавитьКомментарийКТекущемуОбращению(ТекстСообщения, Формат(idЮзера, "ЧГ=0"));	
		ИначеЕсли флВозвращеноВРаботу Тогда
			ТекстСообщения = "Возвращаем вашу заявку в работу.";
			ОтправитьСообщениеТелеграм(idЮзера, ТекстСообщения, СостояниеТекущиеОбращение);
			УстановитьСтатусТекущейЗаявки(Формат(idЮзера, "ЧГ=0"), Ложь);
		ИначеЕсли флЗакрыта Тогда	                                     
			ТекстСообщения = "Спасибо, ваша заявка закрыта.";
			ОтправитьСообщениеТелеграм(idЮзера, ТекстСообщения, СостояниеЗакрытаЗаявка);
			УстановитьСтатусТекущейЗаявки(Формат(idЮзера, "ЧГ=0"), Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого НовоеОбращение ИЗ МассивНовыйЗаявок Цикл
		ТекстСообщения = "Спасибо за ваше обращение. Пожалуйста, опишите возникшую проблему в следующем сообщении.";	
		ОтправитьСообщениеТелеграм(НовоеОбращение, ТекстСообщения, СостояниеОжидаетНовоеОбращение);
	КонецЦикла;
	

 	
КонецПроцедуры     


Процедура ОтправитьСообщениеТелеграм(idПолучателя, ТекстСообщения, СостояниеПользователя = Неопределено) Экспорт
	
	ПараметрыПодключения = ИнтеграцииПовтИсп.ПолучитьНастройкиПодключения(Справочники.НастройкиПодключения.Telegram);
	ТокенБота = ПараметрыПодключения.ТокенДоступа;
	СоединениеHTTP = Новый HTTPСоединение(ПараметрыПодключения.URL, , , , , , новый ЗащищенноеСоединениеOpenSSL); 	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type" , "application/json");
	
	ДанныеДляОтправки = новый Структура;
	ДанныеДляОтправки.Вставить("chat_id", Формат(idПолучателя, "ЧГ=0"));
	ДанныеДляОтправки.Вставить("text",ТекстСообщения);
	
	ЗаписьJSON = новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON,ДанныеДляОтправки);
	ТелоЗапроса = ЗаписьJSON.Закрыть();
	
	ЗапросHTTP = Новый HTTPЗапрос("bot" + ТокенБота + ПараметрыПодключения.ВызываемыеМетоды[1], Заголовки);
	
	ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	Ответ = СоединениеHTTP.ОтправитьДляОбработки(ЗапросHTTP);
	
	Если Ответ.КодСостояния = 200 Тогда
		Если ЗначениеЗаполнено(СостояниеПользователя) Тогда
			НаборЗаписей = РегистрыСведений.СостояниеТГПользователя.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.idUser.Установить(idПолучателя);
			
			Запись = НаборЗаписей.Добавить();
			Запись.idUser = idПолучателя;
			Запись.ТекущееСостояние = СостояниеПользователя;
			
			НаборЗаписей.Записать(Истина);

				
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьНовуюЗаявку(ТекстСообщения, idЮзера) 
	СостояниеТекущиеОбращение = Перечисления.СостоянияПользователей.РаботаетСТекущимОбращением;
	НовыйЭлемент = Справочники.ИсточникиОбращений.СоздатьЭлемент();
	НовыйЭлемент.ИсточникОбращения = Перечисления.ИсточникиОбращений.Телеграм;
	НовыйЭлемент.ТемаОбращения = "Возникла проблема от " + ТекущаяДата();
	НовыйЭлемент.ТекстОбращения = ТекстСообщения;
	НовыйЭлемент.АвторОбращения = idЮзера;
	НовыйЭлемент.Статус = Перечисления.СтатусыОбработки.КОбработке;
	НовыйЭлемент.Записать();
	НовыйЭлемент.Наименование = СтрШаблон("Обращение №%1 ТГ", НовыйЭлемент.Код);
	НовыйЭлемент.Записать();
	ТекстСообщения = "По вашему обращению было создана заявка!";
	ОтправитьСообщениеТелеграм(idЮзера, ТекстСообщения, СостояниеТекущиеОбращение); 
	
	ДатаСеанса = ТекущаяДатаСеанса();
	СтруктураHTTPЗапроса = Новый Структура; 
	
	СтруктураHTTPЗапроса.Вставить("ОбъектЗапроса", НовыйЭлемент.Ссылка);	
	СтруктураHTTPЗапроса.Вставить("Действие", Перечисления.ДействияHTTPЗапросов.ПолучениеДанныхИзТГ);
	СтруктураHTTPЗапроса.Вставить("Код", 200);
	СтруктураHTTPЗапроса.Вставить("Описание", "Источник обращения успешно создан и заполнен данными из ТГ");
	
	СРОО_ОбщегоНазначения.ЗаполнитьСтатусHTTPЗапроса(ДатаСеанса, СтруктураHTTPЗапроса.ОбъектЗапроса, СтруктураHTTPЗапроса);
	
	
	СРОО_ФоновыеЗадания.ОбработкаНовыхОбращений(НовыйЭлемент.Ссылка, Формат(idЮзера, "ЧГ=0"));
	
КонецПроцедуры

Процедура ДобавитьКомментарийКТекущемуОбращению(Комментарий, idЮзера) Экспорт
	
	//Получим последнее обращение пользователя
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(Обращения.Ссылка) КАК Ссылка
	               |ИЗ
	               |	Справочник.Обращения КАК Обращения
	               |ГДЕ
	               |	Обращения.ЧатИД = &ЧатИд"; 
	Запрос.УстановитьПараметр("ЧатИД", idЮзера);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
	НСтр = ДокументОбъект.Комментарии.Добавить();
	НСтр.Дата = ТекущаяДата();
	НСтр.Комментарий = Комментарий;
	НСтр.Автор = idЮзера;
	НСтр.Внешний = Истина;
	
	ДокументОбъект.Записать();
	
	ДатаСеанса = ТекущаяДатаСеанса();
	СтруктураHTTPЗапроса = Новый Структура; 
	
	СтруктураHTTPЗапроса.Вставить("ОбъектЗапроса", ДокументОбъект.Ссылка);	
	СтруктураHTTPЗапроса.Вставить("Действие", Перечисления.ДействияHTTPЗапросов.ОбновлениеОбращенийДаннымиИзТГ);
	СтруктураHTTPЗапроса.Вставить("Код", 200);
	СтруктураHTTPЗапроса.Вставить("Описание", "Обращение успешно обновленно данными из ТГ");
	
	СРОО_ОбщегоНазначения.ЗаполнитьСтатусHTTPЗапроса(ДатаСеанса, СтруктураHTTPЗапроса.ОбъектЗапроса, СтруктураHTTPЗапроса);
	
	ТекстСообщения = "Успешно прикрепили ваше сообщение к текущей заявке";
	ОтправитьСообщениеТелеграм(idЮзера, ТекстСообщения); 
КонецПроцедуры

Процедура УстановитьСтатусТекущейЗаявки(idЮзера, флЗакрыть)
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(Обращения.Ссылка) КАК Ссылка
	               |ИЗ
	               |	Справочник.Обращения КАК Обращения
	               |ГДЕ
	               |	Обращения.ЧатИД = &ЧатИд"; 
	Запрос.УстановитьПараметр("ЧатИД", idЮзера);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
	Если флЗакрыть Тогда
		Статус = Перечисления.СтатусыОбращения.Закрыто;
	Иначе
		Статус = Перечисления.СтатусыОбращения.ВозвращеноКлиентом;
	КонецЕсли;
	ДокОбъект.Стаус = Статус;
	ДокОбъект.Записать();
КонецПроцедуры
	
