// Процедура для рассылки текстовых сообщений в телеграм.
// Параметры:
//  idЧата			 - 	Строка - Например, для чата "https://web.telegram.org/k/#-4773128577" id чата будет "-4773128577". 
//  ТекстСообщения	 - Строка - Сообщение для отправки.
//  ТокенДоступаБота - Строка - Токен, который выдал @botfather в телеграмме.
//
Процедура ОтправитьТекстовоеСообщение(idЧата, ТекстСообщения, ТокенДоступаБота) Экспорт
	APITelegram = Справочники.РВ_НастройкиАккаунтов.ПодключениеTelegram.Адрес;
	
	Соединение = Новый HTTPСоединение(APITelegram, 443, , , , , Новый ЗащищенноеСоединениеOpenSSL());
	
	ЧатID = idЧата;
	
	Данные = новый структура;
	Данные.Вставить("chat_id",ЧатID);
	Данные.Вставить("text",ТекстСообщения);
	
	ЗаписьJSON = новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON,Данные);
	
	Строказапроса = ЗаписьJSON.Закрыть();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", "application/json");
	Запрос=Новый HTTPЗапрос("bot" + ТокенДоступаБота + "/sendMessage", Заголовки);
	Запрос.УстановитьТелоИзСтроки(строказапроса);
	
	Ответ=Соединение.ОтправитьДляОбработки(Запрос);
	
	Если НЕ Ответ.КодСостояния = 200 Тогда
		ЗаписьЖурналаРегистрации("РассылкаСообщений", , , , Ответ.ПолучитьТелоКакСтроку());
	КонецЕсли;
	
КонецПроцедуры

// Процедура для рассылки вложений в телеграм.
// Параметры:
//  ТокенДоступаБота - Строка - Токен, который выдал @botfather в телеграмме.
//  idЧата			 - 	Строка - Например, для чата "https://web.telegram.org/k/#-4773128577" id чата будет "-4773128577". 
//  ПутьКФайлу		 - 	Строка - Путь к файлу на сервере. Получать с помощью метода ПолучитьИмяВременногоФайла(). 
//  ИмяФайла		 - Строка - Если ИмяФайла = Неопределено, тогда оно берется из пути к файлу. Важно! ИмяФайла нужно указывать с расширением.
//
Процедура ОтправитьВложение(ТокенДоступаБота, idЧата, ПутьКФайлу, ИмяФайла = Неопределено) Экспорт

	МойToken = ТокенДоступаБота;
	АдресTelegramAPI = Справочники.РВ_НастройкиАккаунтов.ПодключениеTelegram.Адрес;
	
	ЧатID = idЧата;
	
	Boundary = "----"+Строка(Новый УникальныйИдентификатор());

	Файл = Новый Файл(ПутьКФайлу);
	ДвоичныеДанные = новый ДвоичныеДанные(ПутьКФайлу);
    ТелоЗапроса = Новый ПотокВПамяти();
	//Определяем массив для процедуры ОбъединитьФайлы
	ИмяФайлаОтправки = ?(ИмяФайла = Неопределено, Файл.Имя, ИмяФайла);
	ЗаписьДанных = Новый ЗаписьДанных(ТелоЗапроса, КодировкаТекста.UTF8,,Символы.ВК + Символы.ПС, "");
	
	ЗаписьДанных.ЗаписатьСтроку("--" + Boundary);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""chat_id""");
	ЗаписьДанных.ЗаписатьСтроку("");
	ЗаписьДанных.ЗаписатьСтроку(ЧатID);
	ЗаписьДанных.ЗаписатьСтроку("--" + Boundary);
	ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""document""; filename=""" + ИмяФайлаОтправки + """");
	ЗаписьДанных.ЗаписатьСтроку(""); 
	ЗаписьДанных.Записать(ДвоичныеДанные);
	ЗаписьДанных.ЗаписатьСтроку(""); 
	ЗаписьДанных.ЗаписатьСтроку("--" + Boundary + "--");
	ЗаписьДанных.ЗаписатьСтроку(""); 
	ЗаписьДанных.Закрыть();
	
	ДанныеТела = ТелоЗапроса.ЗакрытьИПолучитьДвоичныеДанные();
	
	СоединениеHTTP = Новый HTTPСоединение(АдресTelegramAPI,443,,,,6000,Новый ЗащищенноеСоединениеOpenSSL());

	АдресЗапроса = "bot" 
	                + МойToken 
	                + "/sendDocument";
					
	ЗапросHTTP = Новый HTTPЗапрос(АдресЗапроса);
	ЗапросHTTP.Заголовки.Вставить("Connection", "keep-alive");
	ЗапросHTTP.Заголовки.Вставить("Content-Type", "multipart/form-data; boundary="+Boundary);

	ЗапросHTTP.УстановитьТелоИзДвоичныхДанных(ДанныеТела);

	ОтветHTTP = СоединениеHTTP.ОтправитьДляОбработки(ЗапросHTTP);
			
	Если НЕ ОтветHTTP.КодСостояния = 200 Тогда
		ЗаписьЖурналаРегистрации("РассылкаСообщений", , , , ОтветHTTP.ПолучитьТелоКакСтроку());
	КонецЕсли;
		
	

	
КонецПроцедуры
//Токен доступа бота - Строка
Функция ПолучитьСообщения(ТокенДоступаБота)Экспорт

	Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , , Новый ЗащищенноеСоединениеOpenSSL());
	Заголовки   = Новый Соответствие;
	Заголовки.Вставить("content-type", "application/json");
	Запрос = Новый HTTPЗапрос("bot" + ТокенДоступаБота + "/getUpdates", Заголовки);
	Ответ = Соединение.Получить(Запрос);
	ОтветСтрока = ответ.ПолучитьТелоКакСтроку();
	ЧтениеJSON	= Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ОтветСтрока);	
	Данные = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	Возврат Данные;   

КонецФункции