#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПолеHTML = СформироватьИнтерфейсНаСервере();
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура МониторингSLA(Команда)
	
	ПараметрыОткрытия = Новый Структура("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.МониторингSLA.Форма",ПараметрыОткрытия,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура СостояниеОбращений(Команда)
	ПараметрыОткрытия = Новый Структура("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.СостояниеОбращений.Форма",ПараметрыОткрытия,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ТипыОбращений(Команда)
	ПараметрыОткрытия = Новый Структура("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.ТипыОбращений.Форма",ПараметрыОткрытия,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыОбращений(Команда)
	ПараметрыОткрытия = Новый Структура("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.ТрудозатратыОбращений.Форма",ПараметрыОткрытия,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеМетрик(Команда)
	ПараметрыОткрытия = Новый Структура("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.ВыполнениеМетрик.Форма",ПараметрыОткрытия,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПолеHTMLДокументСформирован(Элемент)
	ПодключитьОбработчикОжидания("ПолучитьИОбработатьДанные", 1, Ложь);	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьИОбработатьДанные() Экспорт
	ОкноДокумента = ЭтаФорма.Элементы.ПолеHTML.Документ.defaultView;
	Если ОкноДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ОкноДокумента.getQueueHead) <> Неопределено И ТипЗнч(ОкноДокумента.getQueueTail) <> Неопределено Тогда
		qh = ЧислоБезопасно(ОкноДокумента.getQueueHead());
		qt = ЧислоБезопасно(ОкноДокумента.getQueueTail());
	Иначе
		qh = ЧислоБезопасно(ОкноДокумента.queueHead);
		qt = ЧислоБезопасно(ОкноДокумента.queueTail);
	КонецЕсли;
	Пока qh < qt Цикл
		Индекс = qh + 1;
		СобытиеJSON = ОкноДокумента.getEvent(Индекс);
		
		Чт = Новый ЧтениеJSON;
		Чт.УстановитьСтроку(СобытиеJSON);
		Данные = ПрочитатьJSON(Чт);
		Если Данные = Неопределено Тогда
			Возврат;	
		КонецЕсли;
		
		УИДСтрокой = Данные.itemId; 
		Если Данные.event = "DropItem" Тогда 
			Статус = ПолучитьСтатусПоСтроке(Данные.categoryId);
			РаботаСсылкой(УИДСтрокой, Ложь, Статус);
		Иначе  
			СсылкаОбр = РаботаСсылкой(УИДСтрокой, Истина);
			ПараметрыФормы = Новый Структура("Ключ", СсылкаОбр);
			ОткрытьФорму("Справочник.Обращения.Форма.ФормаЭлемента", ПараметрыФормы);
		КонецЕсли;
		
		// Сдвигаем указатель в JS
		ОкноДокумента.queueHead = Индекс;
		// Обновляем локальные значения для условия цикла
		qh = Индекс;
		Если ТипЗнч(ОкноДокумента.getQueueTail) <> Неопределено Тогда
			qt = ЧислоБезопасно(ОкноДокумента.getQueueTail());
		Иначе
			qt = ЧислоБезопасно(ОкноДокумента.queueTail);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция РаботаСсылкой(СтрокаУИД, ОткрытьФорму, Статус = Неопределено) 
	ГУИД = новый УникальныйИдентификатор(СтрокаУИД);
	СсылкаОбращений = Справочники.Обращения.ПолучитьСсылку(ГУИД);
	Если ОткрытьФорму Тогда 
		Возврат СсылкаОбращений;
	Иначе
		Об = СсылкаОбращений.ПолучитьОбъект();
		Об.Статус = Статус;
		СРОО_ОбщегоНазначения.ЗаписатьСостояниеОбращенийНаСервере(Об);
		Об.Записать();
	КонецЕсли; 
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтатусПоСтроке(СтатусСтрокой) 
    Если СтатусСтрокой = "Новый" Тогда 
        Возврат Перечисления.СтатусыОбращения.Новое;
    ИначеЕсли СтатусСтрокой = "ВРаботе" Тогда  
        Возврат Перечисления.СтатусыОбращения.ВРаботе;
    ИначеЕсли СтатусСтрокой = "ОжидаетКлиента" Тогда 
        Возврат Перечисления.СтатусыОбращения.ОжидаетКлиента;
    ИначеЕсли СтатусСтрокой = "ВозвращеноКлиентом" Тогда
        Возврат Перечисления.СтатусыОбращения.ВозвращеноКлиентом;
    ИначеЕсли СтатусСтрокой = "Закрыто" Тогда
        Возврат Перечисления.СтатусыОбращения.Закрыто;
    КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ПрочитатьДанныеИзJSON(СтрокаJSON)
	Результат = ПрочитатьСтрокуJSON(СтрокаJSON);
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		Возврат ПрочитатьСтрокуJSON(Результат);
	Иначе
		Возврат Результат;
	КонецЕсли; 
КонецФункции

&НаСервереБезКонтекста
Функция ПрочитатьСтрокуJSON(СтрокаJSON) Экспорт
	Результат = Неопределено;
	Попытка
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		Результат = ПрочитатьJSON(ЧтениеJSON, Истина);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция ЗаписатьДанныеВJSON(Данные) Экспорт
	Попытка
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON());
		ЗаписатьJSON(ЗаписьJSON, Данные);
		Возврат ЗаписьJSON.Закрыть();
	Исключение
	    Сообщить(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки; 
КонецФункции

#КонецОбласти 

#Область Демо

Функция ЧислоБезопасно(Значение) Экспорт
	Попытка
		Возврат Число(Значение);
	Исключение
		Попытка
			Возврат Число(Строка(Значение));
		Исключение
			Возврат -1;
		КонецПопытки;
	КонецПопытки;
КонецФункции

&НаСервере
Функция СформироватьИнтерфейсНаСервере()

	Шаблон = ПолучитьШаблонHTML();
	Kanban  = СформироватьКанбан();
	
	ОбъектКанбан = РеквизитФормыВЗначение("Объект");
	
	css_body             = ОбъектКанбан.ПолучитьМакет("css_body").ПолучитьТекст();
	css_канбан           = ОбъектКанбан.ПолучитьМакет("css_kanban_container").ПолучитьТекст();
	css_category         = ОбъектКанбан.ПолучитьМакет("css_category").ПолучитьТекст();
	css_items_container  = ОбъектКанбан.ПолучитьМакет("css_items_container").ПолучитьТекст();
	css_item             = ОбъектКанбан.ПолучитьМакет("css_item").ПолучитьТекст();
	css_load_button      = ОбъектКанбан.ПолучитьМакет("css_load_button").ПолучитьТекст();

	ШиринаКолонки = 270; // минимальная ширина трека Grid
	css_category = СтрЗаменить(css_category, "width: 250px", "width: " + ШиринаКолонки + "px");
	
	// Базовые стили из макетов
	CSS = css_body + css_канбан + css_category + css_items_container + css_item + css_load_button; 
	
	// Оверрайды: растяжение колонок, отсутствие горизонтального скролла, фикс шапки, скрытые полосы прокрутки списков
	CSS = CSS + Символы.ПС +
"/* --- overrides: full-width grid + no horizontal scroll --- */" + Символы.ПС +
"html, body{ margin:0; padding:0; width:100%; max-width:100%; overflow-x:hidden; }" + Символы.ПС +
"*, *::before, *::after{ box-sizing:border-box; }" + Символы.ПС +
".kanban-container{ display:grid !important; grid-template-columns: repeat(auto-fit, minmax(" + Строка(ШиринаКолонки) + "px, 1fr)); gap:16px; width:100%; max-width:100%; padding:16px; overflow-x:hidden; }" + Символы.ПС +
".category{ width:auto !important; display:flex; flex-direction:column; overflow-y:hidden !important; margin:0 !important; min-width:0; }" + Символы.ПС +
".category-header{ position:relative; z-index:1; background-color: rgba(255,255,255,0.7); }" + Символы.ПС +
".items-container{ flex:1 1 auto; min-height:0; min-width:0; overflow-y:auto !important; overflow-x:hidden; -ms-overflow-style:none; scrollbar-width:none; }" + Символы.ПС +
".items-container::-webkit-scrollbar{ width:0; height:0; }" + Символы.ПС +
".item{ min-width:0; overflow-wrap:anywhere; word-break:break-word; }" + Символы.ПС;

	КлючНастройкиКлиентскогоПриложения = "Общее/НастройкиКлиентскогоПриложения";
	НастройкаТемы = ХранилищеСистемныхНастроек.Загрузить(КлючНастройкиКлиентскогоПриложения).ТемаКлиентскогоПриложения; 
	
	Если НастройкаТемы = ТемаКлиентскогоПриложения.Темная ИЛИ НастройкаТемы = ТемаКлиентскогоПриложения.Авто Тогда
		// Тёмная тема: красим фон доски и страницы в rgb(29,29,29)
		CSS = CSS + Символы.ПС +
"/* theme: dark */" + Символы.ПС +
"body{ background-color: rgb(26,26,26) !important; background-image:none !important; }" + Символы.ПС +
".kanban-container{ background-color: rgb(26,26,26) !important; }";
	Иначе
		CSS = CSS + Символы.ПС +
"/* theme: dark */" + Символы.ПС +
"body{ background-color: rgb(255,255,255) !important; background-image:none !important; }" + Символы.ПС +
".kanban-container{ background-color: rgb(255,255,255) !important; }";
	
	КонецЕсли;

	JS = ОбъектКанбан.ПолучитьМакет("js").ПолучитьТекст();

	// Автопрокрутка при DnD — крутим именно список задач
	JS = JS + Символы.ПС +
	"<script>function getScrollTarget(categoryEl){var c=categoryEl.querySelector('.items-container');return c||categoryEl;}</script>";

	Возврат СтрШаблон(Шаблон, CSS, Kanban, JS);
КонецФункции

&НаСервере
Функция СформироватьКанбан()
    Категории = Новый Массив;
    ШаблонКатегории = ПолучитьШаблонКатегории(); 
    СтруктураМассивов = СформироватьЗадачиКатегорий(); 
    
    // Оптимизированная палитра
    Категории.Добавить(СтрШаблон(ШаблонКатегории, "Новый", "Новые", 
        СтруктураМассивов.МассивНовый.Массив, СформироватьКнопкуЗагрузки(), "52, 211, 153"));
    
    Категории.Добавить(СтрШаблон(ШаблонКатегории, "ВРаботе", "В работе", 
        СтруктураМассивов.МассивВРаботе.Массив, СформироватьКнопкуЗагрузки(), "251, 191, 36"));
    
    Категории.Добавить(СтрШаблон(ШаблонКатегории, "ОжидаетКлиента", "Ожидает клиента", 
        СтруктураМассивов.МассивОжидаетКлиента.Массив, СформироватьКнопкуЗагрузки(), "167, 139, 250"));
    
    Категории.Добавить(СтрШаблон(ШаблонКатегории, "ВозвращеноКлиентом", "Возвращено клиентом", 
        СтруктураМассивов.МассивВозвращеноКлиентом.Массив, СформироватьКнопкуЗагрузки(), "251, 113, 133"));
    
    Категории.Добавить(СтрШаблон(ШаблонКатегории, "Закрыто", "Закрыто", 
        СтруктураМассивов.МассивЗакрыто.Массив, СформироватьКнопкуЗагрузки(), "148, 163, 184"));
    
    Возврат СтрСоединить(Категории, Символы.ПС + Символы.ВК);
КонецФункции

&НаСервере
Функция СформироватьКнопкуЗагрузки(Вывести = Ложь)
	Если Вывести Тогда
		Возврат ПолучитьШаблонКнопкиЗагрузки();
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

&НаСервере
Функция СформироватьЗадачиКатегорий()

	ШаблонЭлемента = ПолучитьШаблонЭлемента();
	МассивНовый = Новый Массив;
	МассивВРаботе = Новый Массив;
	МассивОжидаетКлиента = Новый Массив;
	МассивВозвращеноКлиентом = Новый Массив;
	МассивЗакрыто = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Обращения.Ссылка КАК Ссылка,
		|	Обращения.Источник КАК Источник,
		|	Обращения.Тип КАК Тип,
		|	Обращения.Код КАК Код,
		|	Обращения.Статус КАК Статус,
		|	Обращения.Приоритет КАК Приоритет,
		|	Обращения.Тональность КАК Тональность,
		|	Обращения.Наименование КАК Наименование
		|ИЗ
		|	Справочник.Обращения КАК Обращения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.Статус = Перечисления.СтатусыОбращения.Новое Тогда
			МассивНовый.Добавить(СтрШаблон(ШаблонЭлемента, XMLСтрока(ВыборкаДетальныеЗаписи.Ссылка), СтрШаблон("%1 %2", ВыборкаДетальныеЗаписи.Код,ВыборкаДетальныеЗаписи.Наименование), ВыборкаДетальныеЗаписи.Приоритет, ВыборкаДетальныеЗаписи.Тональность, ВыборкаДетальныеЗаписи.Тип, ВыборкаДетальныеЗаписи.Источник));
		ИначеЕсли ВыборкаДетальныеЗаписи.Статус = Перечисления.СтатусыОбращения.ВРаботе Тогда
			МассивВРаботе.Добавить(СтрШаблон(ШаблонЭлемента, XMLСтрока(ВыборкаДетальныеЗаписи.Ссылка), СтрШаблон("%1 %2", ВыборкаДетальныеЗаписи.Код,ВыборкаДетальныеЗаписи.Наименование), ВыборкаДетальныеЗаписи.Приоритет, ВыборкаДетальныеЗаписи.Тональность, ВыборкаДетальныеЗаписи.Тип, ВыборкаДетальныеЗаписи.Источник));
		ИначеЕсли ВыборкаДетальныеЗаписи.Статус = Перечисления.СтатусыОбращения.ОжидаетКлиента Тогда
			МассивОжидаетКлиента.Добавить(СтрШаблон(ШаблонЭлемента, XMLСтрока(ВыборкаДетальныеЗаписи.Ссылка), СтрШаблон("%1 %2", ВыборкаДетальныеЗаписи.Код,ВыборкаДетальныеЗаписи.Наименование), ВыборкаДетальныеЗаписи.Приоритет, ВыборкаДетальныеЗаписи.Тональность, ВыборкаДетальныеЗаписи.Тип, ВыборкаДетальныеЗаписи.Источник));	
		ИначеЕсли ВыборкаДетальныеЗаписи.Статус = Перечисления.СтатусыОбращения.ВозвращеноКлиентом Тогда
            МассивВозвращеноКлиентом.Добавить(СтрШаблон(ШаблонЭлемента, XMLСтрока(ВыборкаДетальныеЗаписи.Ссылка), СтрШаблон("%1 %2", ВыборкаДетальныеЗаписи.Код,ВыборкаДетальныеЗаписи.Наименование), ВыборкаДетальныеЗаписи.Приоритет, ВыборкаДетальныеЗаписи.Тональность, ВыборкаДетальныеЗаписи.Тип, ВыборкаДетальныеЗаписи.Источник));
		ИначеЕсли ВыборкаДетальныеЗаписи.Статус = Перечисления.СтатусыОбращения.Закрыто Тогда
			МассивЗакрыто.Добавить(СтрШаблон(ШаблонЭлемента, XMLСтрока(ВыборкаДетальныеЗаписи.Ссылка), СтрШаблон("%1 %2", ВыборкаДетальныеЗаписи.Код,ВыборкаДетальныеЗаписи.Наименование), ВыборкаДетальныеЗаписи.Приоритет, ВыборкаДетальныеЗаписи.Тональность, ВыборкаДетальныеЗаписи.Тип, ВыборкаДетальныеЗаписи.Источник));			
		КонецЕсли;
	КонецЦикла;
	
	СтруктураМассивов = Новый Структура;
	СтруктураМассивов.Вставить("МассивНовый", Новый Структура("Массив, Колво",СтрСоединить(МассивНовый, Символы.ПС + Символы.ВК), МассивНовый.Количество()));
	СтруктураМассивов.Вставить("МассивВРаботе", Новый Структура("Массив, Колво",СтрСоединить(МассивВРаботе, Символы.ПС + Символы.ВК), МассивВРаботе.Количество()));
	СтруктураМассивов.Вставить("МассивОжидаетКлиента", Новый Структура("Массив, Колво",СтрСоединить(МассивОжидаетКлиента, Символы.ПС + Символы.ВК), МассивОжидаетКлиента.Количество()));
	СтруктураМассивов.Вставить("МассивВозвращеноКлиентом", Новый Структура("Массив, Колво",СтрСоединить(МассивВозвращеноКлиентом, Символы.ПС + Символы.ВК), МассивВозвращеноКлиентом.Количество()));
	СтруктураМассивов.Вставить("МассивЗакрыто", Новый Структура("Массив, Колво",СтрСоединить(МассивЗакрыто, Символы.ПС + Символы.ВК), МассивЗакрыто.Количество()));
	
	Возврат СтруктураМассивов;
КонецФункции

&НаСервере
Функция СформироватьЗадачиКатегории_2()
	ЗадачиКатегории = Новый Массив;
	ШаблонЭлемента = ПолучитьШаблонЭлемента();
	
	ЗадачиКатегории.Добавить(СтрШаблон(ШаблонЭлемента, Строка(Новый УникальныйИдентификатор()), "Стили", "Добавить регулировку ширины колонок", Формат(200, "ЧРГ=''; ЧГ=0")));
	ЗадачиКатегории.Добавить(СтрШаблон(ШаблонЭлемента, Строка(Новый УникальныйИдентификатор()), "Сортировка", "Исправить добавление вниз списка", Формат(10, "ЧРГ=''; ЧГ=0")));
	
	Возврат СтрСоединить(ЗадачиКатегории, Символы.ПС + Символы.ВК);
КонецФункции

#КонецОбласти 

#Область HTML

Функция ПолучитьШаблонHTML() Экспорт
	
	ШаблонHTML = "
	|<!DOCTYPE html>
	|<html lang=""ru"">
	|<head>
	|  <meta charset=""UTF-8"">
	|  <meta name=""viewport"" content=""width=device-width, initial-scale=1, maximum-scale=1, user-scalable=no, minimal-ui"">
	|  <title>Kanban Board</title>
	|  <style>
	|    %1
	|  </style>
	|</head>
	|<body>
	|
	|<div class=""kanban-container"">
	|%2
	|</div>
	|
	|%3
	|
	|</body>
	|</html>";
	
	Возврат ШаблонHTML;
КонецФункции

Функция ПолучитьШаблонКатегории() Экспорт
	
	// %1 = data-category-id
	// %2 = Заголовок колонки
	// %3 = HTML карточек
	// %4 = Кнопка "Загрузить ещё" (или пусто)
	// %5 = Цвет "R, G, B"
	
	Шаблон =
	"<div class=""category"" data-category-id=""%1""" + Символы.ПС +
	"     ondragenter=""onDragEnter(event)"" ondragleave=""onDragLeave(event)""" + Символы.ПС +
	"     ondragover=""onDragOver(event)""  ondrop=""onDrop(event)""" + Символы.ПС +
	"     style=""background:rgba(%5, 0.5);"">" + Символы.ПС +
	"  <div class=""category-header"">" + Символы.ПС +
	"    <h3>%2</h3>" + Символы.ПС +
	"    <p class=""total-sum"">Количество обращений: </p>" +
	"  </div>" + Символы.ПС +
	"  <div class=""items-container"">" + Символы.ПС +
	"    %3" + Символы.ПС +
	"    %4" + Символы.ПС +
	"  </div>" + Символы.ПС +
	"</div>";
	
	Возврат Шаблон;

КонецФункции

Функция ПолучитьШаблонЭлемента() Экспорт
	
	ШаблонHTML = "
      |<div class=""item"" draggable=""true"" data-id=""%1"" data-amount=""%4"" ondragstart=""onDragStart(event)"" ondragend=""onDragEnd(event)"" onclick=""onClickItem(event)"">
      |  <h4 class=""item-header"">%2</h4>
      |  <p class=""item-desk"">
      |    Приоритет: %3<br>
      |    Тональность: %4<br>
	  |    Тип: %5<br>	
	  |    Источник: %6<br>			
      |  </p>
      |</div>";
	
	Возврат ШаблонHTML;
КонецФункции

Функция ПолучитьШаблонКнопкиЗагрузки() Экспорт
	
	ШаблонHTML = "<button class=""load-more-button"" onclick=""onLoadMoreClick(this)"">Загрузить еще</button>";
	
	Возврат ШаблонHTML;
КонецФункции

#КонецОбласти